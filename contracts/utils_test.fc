{-
    utils_test.fc 

    Tests for utils.fc
-}
;; uri: "test", description: "test test"
(cell) __test_dict_1() asm "B{b5ee9c724101050100620002012001030143bff872ebdb514d9c97c283b7f0ae5179029e2b6119c39462719e4f46ed8f7413e64002000a00746573740143bfe48237bd1bd68753e77399aacc27d2a144c17c59be47bde7648fbd638d3e688240040014007465737420746573749fc649ab} B>boc PUSHREF";
;; publicKey: "test", description: "test test"
(cell) __test_dict_2() asm "B{b5ee9c724101050100620002012001030143bff0f96020ea74f07aac572f746b56db173cdd66722df3685b685df44a3faf86ecc002000a00746573740143bfe48237bd1bd68753e77399aacc27d2a144c17c59be47bde7648fbd638d3e68824004001400746573742074657374573cb0d0} B>boc PUSHREF";
;; uri: "test", publicKey: "test", description: "test test"
(cell) __test_dict_3() asm "B{b5ee9c7241010701008a00020120010502016e02030141bf07cb010753a783d562b97ba35ab6d8b9e6eb33916f9b42db42efa251fd7c3766040141bf03975eda8a6ce4be141dbf85728bc814f15b08ce1ca3138cf27a376c7ba09f3204000a00746573740143bfe48237bd1bd68753e77399aacc27d2a144c17c59be47bde7648fbd638d3e688240060014007465737420746573746f198f43} B>boc PUSHREF";
;; symbol: "TST", decimals: "12"
(cell) __test_dict_4() asm "B{b5ee9c724101050100580002015801030142bfb76a7ca153c24671658335bbd08946350ffc621fa1c516e7123095d4ffd5c581020008005453540142bfae80fd2f1e03480e2282363596ee752d7bb27f50776b95086a0279189675923e040006003132cb541380} B>boc PUSHREF";

_ test_utils::address_to_hex_string() impure inline {
    assert("address_to_hex_string #0", equal_slices(utils::address_to_hex_string(0), "0000000000000000000000000000000000000000000000000000000000000000"));
}

_ test_utils::number_to_slice() impure inline {
    assert("number_to_slice #0", equal_slices(utils::number_to_slice(0), "0"));
    assert("number_to_slice #1", equal_slices(utils::number_to_slice(10), "10"));
    assert("number_to_slice #2", equal_slices(utils::number_to_slice(-10), "-10"));
    assert("number_to_slice #3", equal_slices(utils::number_to_slice(-0), "0"));
}

_ test_utils::merge_dicts() impure inline {
    cell dict1 = __test_dict_1();
    cell dict2 = __test_dict_2();
    cell dict3 = __test_dict_3();
    cell dict4 = __test_dict_4();

    assert("merge_dicts #0", equal_cells(utils::merge_dicts(dict1, dict1, 256), dict1));
    assert("merge_dicts #1", equal_cells(utils::merge_dicts(dict1, dict2, 256), dict3));
    assert("merge_dicts #2", equal_cells(utils::merge_dicts(dict3, dict1, 256), dict3));
    assert("merge_dicts #3", ~ equal_cells(utils::merge_dicts(dict1, dict2, 256), dict4));
    assert("merge_dicts #4", ~ equal_cells(utils::merge_dicts(dict4, dict2, 256), dict4));
    assert("merge_dicts #5", equal_cells(utils::merge_dicts(new_dict(), dict1, 256), dict1));
    assert("merge_dicts #6", equal_cells(utils::merge_dicts(dict1, new_dict(), 256), dict1));
}

_ test_utils::to_coins() impure inline {
    assert("to_coins #0", utils::to_coins(0) == 0);
    assert("to_coins #1", utils::to_coins(1) == 1000000000);
    assert("to_coins #2", utils::to_coins(1000000000) == 1000000000000000000);
}

_ test_utils::send_excesses() impure inline {
    ;; Assertion is done in Sandbox environment
    utils::send_excesses(0, ctx.at(SENDER), QCARRY_REMAINING_GAS);
}

_ test_utils::make_c7() impure inline {
    tuple c7 = utils::make_c7();
    tuple stack = c7.at(0);

    assert("make_c7 #0", stack.at(0) == 0x076ef1ea);
    assert("make_c7 #1", stack.at(1) == 0);
    assert("make_c7 #2", stack.at(2) == 0);
    assert("make_c7 #3", stack.at(3) == now());
    assert("make_c7 #4", stack.at(4) == 1);
    assert("make_c7 #5", stack.at(5) == 1);
    assert("make_c7 #6", stack.at(6) == get_seed());
    assert("make_c7 #7", stack.at(7).at(0) == 1000000000 & is_null(stack.at(7).at(1)));
    assert("make_c7 #8", equal_slices(stack.at(8), my_address()));
    assert("make_c7 #9", equal_cells(stack.at(9), get_config()));
}

cell pack_state_init(cell code, cell data) inline {
    return begin_cell()
        .store_uint(0, 1 + 1)
        .store_dict(code)
        .store_dict(data)
        .store_uint(0, 1)
        .end_cell();
}

slice calc_address_simple(cell code, cell data, int workchain) inline {
    return begin_cell().store_uint(4, 3)
            .store_int(workchain, 8)
            .store_uint(cell_hash(pack_state_init(code, data)), 256)
        .as_slice();
}

_ test_utils::calc_account_hash() impure inline {
    cell code = my_code();
    cell data = begin_cell().store_uint(123, 8).store_uint(456, 256).end_cell();

    assert("calc_account_hash #0", cell_hash(pack_state_init(code, data)) == utils::calc_account_hash(code, data));
}

_ test_utils::basechain_address_from_code_and_data() impure inline {
    cell code = my_code();
    cell data = begin_cell().store_uint(123, 8).store_uint(456, 256).end_cell();

    assert("basechain_address_from_code_and_data #0", equal_slices(calc_address_simple(code, data, 0), utils::basechain_address_from_code_and_data(code, data)));
}

_ test_utils() impure inline_ref {
    test_utils::address_to_hex_string();
    test_utils::number_to_slice(); 
    test_utils::merge_dicts();
    test_utils::to_coins();
    test_utils::send_excesses();
    test_utils::make_c7();
    test_utils::calc_account_hash();
    test_utils::basechain_address_from_code_and_data();
}